load("//py:defs.bzl", "py_binary", "py_library", "py_test")
load("@python_toolchain//:defs.bzl", "host_platform")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@pypi//:requirements.bzl", "requirement")
load("//py/tests/external-deps/custom-macro:macro.bzl", "click_cli_binary")

py_library(
    name = "lib",
    srcs = ["lib.py"],
    deps = [
        "@pypi_colorama//:wheel",
        requirement("types-colorama"),
    ],
)

py_binary(
    name = "pathing",
    srcs = ["pathing.py"],
    deps = [
        ":lib",
        "@pypi_django//:wheel",
        requirement("django-types"),
        # TODO(alex): shouldn't need to repeat this, as :lib depends on it
        requirement("types-colorama"),
    ],
)

genrule(
    name = "run_pathing",
    outs = ["pathing_out"],
    cmd = "|".join([
        "$(execpath pathing)",
        """sed "s#$$(pwd)#(pwd)#" """,
        # scrub various platform-specific paths, so our expected_pathing file is portable
        "sed 's#^.*execroot/_main/external/rules_python~[0-9.]*~python~python_3_9_%s#(py_toolchain)#'" % host_platform,
        "sed 's#^/tmp/bazel-source-roots/[0-9]#(py_toolchain)#'",
        "sed 's#bazel-out/[_a-z0-9-]*-exec-[A-Za-f0-9-]*/bin/#bazel-out/[exec]/bin/#'",
        "sed 's#(main, .*)#(main, REDACTED)#'",
    ]) + "> $@",
    tools = ["pathing"],
)

py_binary(
    name = "click",
    srcs = ["//py/tests/external-deps/custom-macro:__main__.py"],
    deps = [
        # Referencing the external py_library rule generated by rules_python
        "@pypi_click//:pkg",
    ],
)

genrule(
    name = "run_click",
    outs = ["click_out"],
    cmd = "$(execpath click) --count 314 > $@",
    tools = ["click"],
)

click_cli_binary(
    name = "click_binary",
)

genrule(
    name = "run_click_cli_binary",
    outs = ["click_cli_binary_out"],
    cmd = "$(execpath click_binary) --count 628 > $@",
    tools = ["click_binary"],
)

write_source_files(
    name = "test",
    files = {
        "expected_pathing": "pathing_out",
        "expected_click": "click_out",
        "expected_click_cli_binary": "click_cli_binary_out",
    },
)

py_test(
    name = "test_can_import_runfiles_helper",
    srcs = ["test_can_import_runfiles_helper.py"],
    tags = ["skip-typecheck"],
    deps = [
        "@bazel_tools//tools/python/runfiles",
    ],
)
