load("//py:defs.bzl", "py_binary")
load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_oci//oci:defs.bzl", "oci_image")
load("@container_structure_test//:defs.bzl", "container_structure_test")

py_binary(
    name = "main_bin",
    srcs = ["__main__.py"],
    tags = ["manual"],
    deps = [
        "//py/tests/containers/branding",
        "//py/tests/internal-deps/adder",
        "@pypi_colorama//:pkg",
    ],
)

tar(
    name = "py_layer",
    srcs = [":main_bin"],
)

oci_image(
    name = "image",
    base = "@ubuntu",
    entrypoint = ["/py/tests/containers/main_bin"],
    tars = [":py_layer"],
)

platform(
    name = "aarch64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "x86_64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform_transition_filegroup(
    name = "platform_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": ":aarch64_linux",
        "@platforms//cpu:x86_64": ":x86_64_linux",
    }),
)

container_structure_test(
    name = "test",
    configs = ["py_image_test.yaml"],
    image = ":platform_image",
)